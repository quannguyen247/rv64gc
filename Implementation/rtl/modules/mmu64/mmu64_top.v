`timescale 1ns / 1ps
`include "mmu64_defs.vh"

module mmu64_top #(
    parameter TLB_ENTRIES = `TLB_ENTRIES
) (
    input wire clk,
    input wire rst_n,
    input wire [`VA_WIDTH-1:0] va,
    input wire [1:0] access_type,
    input wire [1:0] priv_mode,
    input wire req,
    output wire ready,
    input wire [63:0] satp,
    input wire mstatus_sum,
    input wire mstatus_mxr,
    output reg [`PA_WIDTH-1:0] pa,
    output reg pa_valid,
    output reg page_fault,
    input wire sfence_vma,
    output wire mem_req,
    output wire [`PA_WIDTH-1:0] mem_addr,
    input wire [`PTE_WIDTH-1:0] mem_rdata,
    input wire mem_valid
);

    localparam [1:0] ST_IDLE = 2'b00;
    localparam [1:0] ST_WALK = 2'b01;

    reg [1:0] state;

    wire [3:0] satp_mode = satp[`SATP_MODE];
    wire [15:0] satp_asid = satp[`SATP_ASID];
    wire [`PPN_WIDTH-1:0] satp_ppn = satp[`SATP_PPN];

    wire translation_en = (satp_mode == `SATP_MODE_SV39) && (priv_mode != `PRIV_M);
    wire va_valid = (va[63:39] == {25{va[38]}});
    wire [`VPN_TOTAL_W-1:0] va_vpn = {va[38:30], va[29:21], va[20:12]};

    reg [`VA_WIDTH-1:0] va_reg;
    reg [15:0] asid_reg;
    reg kill_walk;

    wire [`VPN_TOTAL_W-1:0] va_reg_vpn = {va_reg[38:30], va_reg[29:21], va_reg[20:12]};

    wire tlb_hit;
    wire [`PPN_WIDTH-1:0] tlb_ppn;
    wire [7:0] tlb_flags;
    wire [1:0] tlb_pgsz;

    reg tlb_we;
    reg [`VPN_TOTAL_W-1:0] tlb_wr_vpn;
    reg [`PPN_WIDTH-1:0] tlb_wr_ppn;
    reg [7:0] tlb_wr_flags;
    reg [1:0] tlb_wr_pgsz;

    wire tlb_lookup_req = (state == ST_IDLE) && req && !sfence_vma && translation_en && va_valid;

    mmu64_tlb #(
        .ENTRIES(TLB_ENTRIES)
    ) u_tlb (
        .clk(clk),
        .rst_n(rst_n),
        .lookup_vpn(va_vpn),
        .lookup_req(tlb_lookup_req),
        .lookup_asid(satp_asid),
        .lookup_hit(tlb_hit),
        .lookup_ppn(tlb_ppn),
        .lookup_flags(tlb_flags),
        .lookup_page_size(tlb_pgsz),
        .write_en(tlb_we),
        .write_vpn(tlb_wr_vpn),
        .write_ppn(tlb_wr_ppn),
        .write_flags(tlb_wr_flags),
        .write_page_size(tlb_wr_pgsz),
        .write_asid(asid_reg),
        .flush(sfence_vma)
    );

    wire tlb_perm_fault;
    mmu64_perm_check u_tlb_perm (
        .access_type(access_type),
        .priv_mode(priv_mode),
        .pte_r(tlb_flags[`PTE_R]),
        .pte_w(tlb_flags[`PTE_W]),
        .pte_x(tlb_flags[`PTE_X]),
        .pte_u(tlb_flags[`PTE_U]),
        .pte_a(tlb_flags[`PTE_A]),
        .pte_d(tlb_flags[`PTE_D]),
        .mstatus_sum(mstatus_sum),
        .mstatus_mxr(mstatus_mxr),
        .fault(tlb_perm_fault)
    );

    reg [`PA_WIDTH-1:0] tlb_pa;
    always @(*) begin
        case (tlb_pgsz)
            2'd0: tlb_pa = {tlb_ppn, va[11:0]};
            2'd1: tlb_pa = {tlb_ppn[`PPN_WIDTH-1:9], va[20:0]};
            2'd2: tlb_pa = {tlb_ppn[`PPN_WIDTH-1:18], va[29:0]};
            default: tlb_pa = {tlb_ppn, va[11:0]};
        endcase
    end

    wire walk_start;
    wire walk_done;
    wire walk_fault;
    wire [`PPN_WIDTH-1:0] walk_ppn;
    wire [7:0] walk_flags;
    wire [1:0] walk_pgsz;

    assign walk_start = tlb_lookup_req && !tlb_hit;

    mmu64_walker u_walker (
        .clk(clk),
        .rst_n(rst_n),
        .walk_req(walk_start),
        .vpn(va_vpn),
        .access_type(access_type),
        .priv_mode(priv_mode),
        .satp_ppn(satp_ppn),
        .mstatus_sum(mstatus_sum),
        .mstatus_mxr(mstatus_mxr),
        .walk_done(walk_done),
        .walk_fault(walk_fault),
        .walk_ppn(walk_ppn),
        .walk_flags(walk_flags),
        .walk_page_size(walk_pgsz),
        .mem_req(mem_req),
        .mem_addr(mem_addr),
        .mem_rdata(mem_rdata),
        .mem_valid(mem_valid)
    );

    reg [`PA_WIDTH-1:0] walk_pa;
    always @(*) begin
        case (walk_pgsz)
            2'd0: walk_pa = {walk_ppn, va_reg[11:0]};
            2'd1: walk_pa = {walk_ppn[`PPN_WIDTH-1:9], va_reg[20:0]};
            2'd2: walk_pa = {walk_ppn[`PPN_WIDTH-1:18], va_reg[29:0]};
            default: walk_pa = {walk_ppn, va_reg[11:0]};
        endcase
    end

    assign ready = (state == ST_IDLE) && !sfence_vma;

    always @(posedge clk) begin
        if (!rst_n) begin
            state <= ST_IDLE;
            pa <= {`PA_WIDTH{1'b0}};
            pa_valid <= 1'b0;
            page_fault <= 1'b0;
            tlb_we <= 1'b0;
            va_reg <= {`VA_WIDTH{1'b0}};
            asid_reg <= 16'd0;
            kill_walk <= 1'b0;
            tlb_wr_vpn <= {`VPN_TOTAL_W{1'b0}};
            tlb_wr_ppn <= {`PPN_WIDTH{1'b0}};
            tlb_wr_flags <= 8'h0;
            tlb_wr_pgsz <= 2'd0;
        end else begin
            pa_valid <= 1'b0;
            page_fault <= 1'b0;
            tlb_we <= 1'b0;

            case (state)
                ST_IDLE: begin
                    kill_walk <= 1'b0;
                    if (req && !sfence_vma) begin
                        va_reg <= va;
                        asid_reg <= satp_asid;

                        if (!translation_en) begin
                            pa <= va[`PA_WIDTH-1:0];
                            pa_valid <= 1'b1;
                        end else if (!va_valid) begin
                            page_fault <= 1'b1;
                        end else if (tlb_hit) begin
                            if (tlb_perm_fault) begin
                                page_fault <= 1'b1;
                            end else begin
                                pa <= tlb_pa;
                                pa_valid <= 1'b1;
                            end
                        end else begin
                            state <= ST_WALK;
                        end
                    end
                end

                ST_WALK: begin
                    if (sfence_vma) begin
                        kill_walk <= 1'b1;
                    end

                    if (walk_done) begin
                        if (!kill_walk && !sfence_vma) begin
                            tlb_we <= 1'b1;
                            tlb_wr_vpn <= va_reg_vpn;
                            tlb_wr_ppn <= walk_ppn;
                            tlb_wr_flags <= walk_flags;
                            tlb_wr_pgsz <= walk_pgsz;
                            pa <= walk_pa;
                            pa_valid <= 1'b1;
                        end
                        state <= ST_IDLE;
                    end else if (walk_fault) begin
                        if (!kill_walk && !sfence_vma) begin
                            page_fault <= 1'b1;
                        end
                        state <= ST_IDLE;
                    end
                end

                default: begin
                    state <= ST_IDLE;
                    kill_walk <= 1'b0;
                end
            endcase
        end
    end

endmodule
